/*
 * Copyright (C) 2016-2020 David Rubio Escares / Kodehawa
 *
 *  Mantaro is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *  Mantaro is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Mantaro.  If not, see http://www.gnu.org/licenses/
 */

/* Mantaro's build.gradle */

//Plugins
plugins {
    //Compiles Java
    id 'java'
    //Adds an Executable Manifest
    id 'application'
    //Creates FatJars
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    //Tell me when smth is outdated
    id 'com.github.ben-manes.versions' version '0.28.0'
    //Builds docker images
    id 'com.palantir.docker' version '0.22.1'
}

//Define the Main Class
mainClassName = "net.kodehawa.mantarobot.MantaroBot"

//Use an unified versioning system
def ver = new Version(major: 5, minor: 5, revision: 5)
version ver.toString()

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/kodehawa/maven" }
    maven { url 'https://dl.bintray.com/natanbc/maven' }
    maven { url 'https://maven.notfab.net/Hosted' }
    maven {
        url 'https://dl.bintray.com/sedmelluq/com.sedmelluq'
    }
    maven { url 'https://jitpack.io' }
}

task generateLanguageList {
    def out = new PrintStream(new FileOutputStream("src/main/resources/assets/languages/list.txt"))
    new File("src/main/resources/assets/languages").listFiles().each {
        if(it.getName().endsWith("json")) {
            out.println(it.getName())
        }
    }
    out.close()
}

dependencies {
    //remember to update this 3 all the time lul
    compile 'net.dv8tion:JDA:4.1.1_154'
    compile 'club.minnced:discord-webhooks:0.3.0'

    //Music
    compile ('com.github.FredBoat:Lavalink-Client:f84b333518') {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }

    compile 'com.sedmelluq:lavaplayer:1.3.49'
    compile 'com.sedmelluq:lavaplayer-ext-youtube-rotator:0.2.3'

    compile 'io.github.classgraph:classgraph:4.6.3'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile 'com.google.guava:guava:27.0.1-jre'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.7'

    compile 'com.rethinkdb:rethinkdb-driver:2.4.2'
    compile 'redis.clients:jedis:2.9.0'
    compile 'org.redisson:redisson:3.12.5'

    compile 'io.sentry:sentry:1.7.10'

    compile 'net.jodah:expiringmap:0.5.9'
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'org.apache.commons:commons-text:1.6'

    compile fileTree(dir: 'lib', include: '*.jar')
    compile 'net.kodehawa:imageboard-api:2.2.1'
    compile 'com.github.natanbc:discordbots-api:3.3.3'
    compile 'com.github.natanbc:java-eval:1.0'
    compile 'com.github.natanbc:usage-tracker:0.1.1'
    implementation 'com.github.natanbc:reliqua:1.0'

    //Basically for FinderUtil
    compile 'com.jagrosh:jda-utilities:3.0.1'

    compile 'io.prometheus:simpleclient:0.5.0'
    compile 'io.prometheus:simpleclient_hotspot:0.5.0'
    compile 'io.prometheus:simpleclient_httpserver:0.5.0'

    compile 'net.sf.trove4j:trove4j:3.0.3'

    //Unit tests
    testCompile 'junit:junit:4.12'
}

task copyFileToAssets(type: Copy) {
    from shadowJar.outputs
    into file('assets')
}

copyFileToAssets.dependsOn shadowJar

docker {
    name "mantaro/mantaro:$ver"
    tag "latest", "mantaro/mantaro:latest"
    copySpec.from("assets").into("assets")
    buildArgs([
        version: ver.toString(),
        jattachVersion: "v1.5"
    ])
}

docker.dependsOn shadowJar as Task
docker.dependsOn copyFileToAssets

compileJava.dependsOn generateLanguageList

task ci {
    dependsOn shadowJar
    dependsOn tasks.docker
}

//This doesn't work?
test.dependsOn generateLanguageList

def lint = [
        "auxiliaryclass",
        "cast",
        "classfile",
        "deprecation",
        "dep-ann",
        "divzero",
        "empty",
        "exports",
        "fallthrough",
        "finally",
        "module",
        "opens",
        "options",
        "overloads",
        "overrides",
        "path",
        //removed because of "No processor claimed any of these annotations: ..."
        //"processing",
        "rawtypes",
        "removal",
        "requires-automatic",
        "requires-transitive-automatic",
        "serial",
        "static",
        "try",
        "unchecked",
        "varargs",
        "preview"
]

import org.apache.tools.ant.filters.ReplaceTokens

def gitRevision() {
    def gitVersion = new ByteArrayOutputStream()
    exec {
        commandLine("git", "rev-parse", "--short", "HEAD")
        standardOutput = gitVersion
    }

    return gitVersion.toString().trim()
}

task sourcesForRelease(type: Copy) {
    from ('src/main/java') {
        include '**/MantaroInfo.java'
        filter(ReplaceTokens, tokens: [
                version: ver.toString(),
                revision: gitRevision().toString()
        ])
    }
    into 'build/filteredSrc'

    includeEmptyDirs = false
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'MantaroInfo.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    source = generateJavaSources.source
    classpath = sourceSets.main.compileClasspath
    //options.compilerArgs += ["-Xlint:${lint.join(",")}", "-Werror"]
    options.compilerArgs += ["-Xlint:${lint.join(",")}"]

    dependsOn generateJavaSources
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
}

build.dependsOn shadowJar

shadowJar {
    classifier = null
    exclude 'module-info.class'
}

class Version {
    String major, minor, revision

    String toString() {
        "${major}.${minor}.${revision}"

    }
}
